WHITESPACE = _{ " " | "\t" | "\r" | "\n" | "," }
COMMENT = @{ ";" ~ (!("\n" | "\r") ~ ANY)* }

// From the docs
STRING = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

boolean = @{ ("true" | "false" ) ~ !ident_char }
nil = @{ ("nil") ~ !ident_char }
ident_char = _{ ASCII_ALPHANUMERIC | "_" | "-" | "+" | "/" | "*" | "." }

round = { "(" ~ obj* ~ ")" }
square = { "[" ~ obj* ~ "]" }
curly = { "{" ~ obj* ~ "}" }

number = @{ "-"? ~ ASCII_DIGIT+ }
symbol = @{ !boolean ~ !nil ~ (!SPECIAL_CHAR ~ !WHITESPACE ~ !COMMENT ~ ANY)+ }
SPECIAL_CHAR = { "[" | "]" | "{" | "}" | "(" | ")" | "`" | "~" | "^" | "@" }
NON_SPECIAL_SEQ = @{ (!SPECIAL_CHAR ~ !WHITESPACE ~ !COMMENT ~ ANY)+ }

obj = _{ number | STRING | round | square | curly | symbol | boolean | nil | COMMENT }

mal = { SOI ~ obj* ~ EOI }
